buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.0.9'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'pmd'
apply plugin: 'jacoco'

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
}

mainClassName = 'capstone.mobile.App'

dependencies {
    compile 'com.gluonhq:charm:3.0.0'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
    compile group: 'org.json', name: 'json', version: '20160810'

    androidRuntime 'com.gluonhq:charm-android:3.0.0'
    androidRuntime 'org.sqldroid:sqldroid:1.0.3'
    iosRuntime 'com.gluonhq:charm-ios:3.0.0'
    desktopRuntime 'com.gluonhq:charm-desktop:3.0.0'
    desktopRuntime 'org.xerial:sqlite-jdbc:3.8.11.2'

    testCompile 'junit:junit:4.12'
    testCompile group: 'com.github.seratch', name: 'java-time-backport', version: '1.0.0'
    testCompile group: 'net.java.openjfx.backport', name: 'openjfx-78-backport', version: '1.8.0-ea-b96.1'
}

jfxmobile {
    android {
        manifest = 'src/android/AndroidManifest.xml'
        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE.txt'
        }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.gluonhq.**.*',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'SQLite.**.*',
                'org.javafxports.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}

pmd {
    ignoreFailures = true
    ruleSets = ["java-basic",
                "java-braces",
                "java-naming",
                "java-codesize",
                "java-optimizations",
                "java-unusedcode",
                "java-design",
                "java-junit",
                "java-controversial"]
}

jacoco {
    reportsDir = file("$buildDir/jacocoReport")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

sourceSets {
    test {
        java {
            srcDirs = ["src/tests"]
        }
    }
}

retrolambda {

    jdk System.getenv("JAVA_HOME")
    if (System.getenv("JAVA7_HOME")) {
        oldJdk System.getenv("JAVA7_HOME")
    } else {
        oldJdk "/Library/Java/JavaVirtualMachines/1.6.0.jdk/Contents/Home"
    }
}